MODULE DumpDecl;

IMPORT AST, Lex, Out;

CONST
  DECL_SIZE = 5000;
  TYPE_SIZE = 5000;

VAR
  indent          :INTEGER;
  decls           :ARRAY DECL_SIZE OF AST.Decl;
  types           :ARRAY TYPE_SIZE OF AST.Type;
  lastDecl        :INTEGER;
  lastType        :INTEGER;
  GenDecl         :PROCEDURE (l :Lex.Lexer; e :AST.Decl);

PROCEDURE GenStr(s :ARRAY OF CHAR);
BEGIN
  Out.Str(s);
END GenStr;

PROCEDURE GenInt(i :INTEGER);
BEGIN
  Out.Int(i);
END GenInt;

PROCEDURE GenLn;
BEGIN
  Out.Ln
END GenLn;

PROCEDURE GenI;
VAR
  i :INTEGER;
BEGIN
  FOR i := 1 TO indent DO GenStr("  ") END
END GenI;

PROCEDURE GenDeclKind(kind :INTEGER);
BEGIN
  IF kind = AST.declEND THEN
    GenStr("declEND");
  ELSIF (kind < 1) OR (kind > 9) THEN
    GenStr("INVALID_DECL");
  ELSE
    CASE kind OF
      AST.declImport: GenStr("declImport")
    | AST.declConst: GenStr("declConst")
    | AST.declType: GenStr("declType")
    | AST.declVar: GenStr("declVar")
    | AST.declProcedure: GenStr("declProcedure")
    | AST.declParam: GenStr("declParam")
    | AST.declVarParam: GenStr("declVarParam")
    | AST.declField: GenStr("declField")
    | AST.declBuiltin: GenStr("declBuiltin")
    END
  END;
  GenLn;
END GenDeclKind;

PROCEDURE GenQName(l :Lex.Lexer; name :AST.QName);
BEGIN
  IF name.moduleName > 0 THEN
    Lex.OutStr(l, name.moduleName);
    GenStr(".");
  END;
  IF name.name > 0 THEN
    Lex.OutStr(l, name.name);
  END;
END GenQName;

PROCEDURE GenType(l :Lex.Lexer; t :AST.Type);
VAR
  i :INTEGER;
  found :BOOLEAN;
  f :AST.Decl;
BEGIN
  i := 0; found := FALSE;
  GenI;
  WHILE (i < lastType) & ~found DO
    IF types[i] = t THEN
      found := TRUE;
      GenStr("Already generated type: "); GenInt(i); GenLn;
    END;
    INC(i)
  END;
  IF ~found THEN
    ASSERT(i < TYPE_SIZE);
    types[i] := t;
    GenStr("Type #");
    GenInt(i);
    GenLn;
    INC(lastType);
    GenI; GenStr("kind: ");
    IF (t.kind < AST.typeNil) OR (t.kind > 14) THEN
      GenStr("INVALID_EXPR");
    ELSE
      CASE t.kind OF
        AST.typeNil: GenStr("typeNil")
        | AST.typeInteger: GenStr("typeInteger")
        | AST.typeReal: GenStr("typeReal")
        | AST.typeBoolean: GenStr("typeBoolean")
        | AST.typeChar: GenStr("typeChar")
        | AST.typeSet: GenStr("typeSet")
        | AST.typeString: GenStr("typeString")
        | AST.typePointer: GenStr("typePointer")
        | AST.typeArray: GenStr("typeArray")
        | AST.typeBuffer: GenStr("typeBuffer")
        | AST.typeRecord: GenStr("typeRecord")
        | AST.typeProcedure: GenStr("typeProcedure")
        | AST.typeImport: GenStr("typeImport")
        | 14: GenStr("typeEND")
      END;
    END;
    GenLn;
    IF t.base # NIL THEN
      GenI; GenStr("base:"); GenLn;
      INC(indent);
      GenType(l, t.base);
      DEC(indent);
    END;
    GenI; GenStr("length: "); GenInt(t.length); GenLn;
    GenI; GenStr("needTypeInfo: ");
    IF t.needTypeInfo THEN GenStr("TRUE") ELSE GenStr("FALSE") END;
    GenLn;
    IF t.decl # NIL THEN
      GenI; GenStr("decl:"); GenLn;
      INC(indent);
      GenDecl(l, t.decl);
      DEC(indent)
    END;
    GenI; GenStr("typeId: "); GenInt(t.typeId); GenLn;
    GenI; GenStr("reachable: ");
    IF t.reachable THEN GenStr("TRUE") ELSE GenStr("FALSE") END;
    GenLn;
    i := 0;
    f := t.fields;
    WHILE f # NIL DO
      IF f.kind = AST.declEND THEN
        ASSERT(f.next = NIL)
      END;
      GenI; GenStr("Field #"); GenInt(i); GenLn;
      GenDecl(l, f);
      INC(i);
      f := f.next;
    END
  END
END GenType;

PROCEDURE GenExpr(l :Lex.Lexer; e :AST.Expr);
BEGIN
  GenI; GenStr("kind: ");
  IF (e.kind < AST.exprVal) OR (e.kind > AST.exprAssign) THEN
    GenStr("INVALID_EXPR");
  ELSE
    CASE e.kind OF
      AST.exprVal: GenStr("exprVal")
      | AST.exprUnary: GenStr("exprUnary")
      | AST.exprBinary: GenStr("exprBinary")
      | AST.exprIdentRef: GenStr("exprIdentRef")
      | AST.exprProcCall: GenStr("exprProcCall")
      | AST.exprFieldRef: GenStr("exprFieldRef")
      | AST.exprPointerDeref: GenStr("exprPointerDeref")
      | AST.exprArrayRef: GenStr("exprArrayRef")
      | AST.exprBufferRef: GenStr("exprBufferRef")
      | AST.exprTypeGuard: GenStr("exprTypeGuard")
      | AST.exprBuiltin: GenStr("exprBuiltin")
      | AST.exprAssign: GenStr("exprAssign")
    END
  END;
  GenLn;
  IF (e.op > 0) & (e.op < 71) THEN
    GenI; GenStr("op: "); GenInt(e.op); GenStr(" (");
    GenStr(Lex.TokenNames[e.op]); GenStr(")"); GenLn;
  END;
  IF e.typ # NIL THEN
    GenI;
    GenStr("type:");
    GenLn;
    INC(indent);
    GenType(l, e.typ);
    DEC(indent)
  END;
  IF e.lhs # NIL THEN
    GenI; GenStr("lhs:"); GenLn;
    INC(indent);
    GenExpr(l, e.lhs);
    DEC(indent);
  END;
  IF e.rhs # NIL THEN
    GenI; GenStr("rhs:"); GenLn;
    INC(indent);
    GenExpr(l, e.rhs);
    DEC(indent);
  END;
  IF e.next # NIL THEN
    GenI; GenStr("next:"); GenLn;
    INC(indent);
    GenExpr(l, e.next);
    DEC(indent);
  END;
  IF e.decl # NIL THEN
    GenI; GenStr("decl: "); GenLn;
    INC(indent);
    GenDecl(l, e.decl);
    DEC(indent)
  END;
  IF e.iVal > 0 THEN GenI; GenStr("iVal: "); GenInt(e.iVal); GenLn END
END GenExpr;

PROCEDURE GenStmt(l :Lex.Lexer; s :AST.Stmt);
VAR
  i :INTEGER;
  c :AST.Stmt;
BEGIN
  i := 0; c := s;
  WHILE c # NIL DO
    GenI; GenStr("Statement "); GenInt(i); GenLn;
    GenI; GenStr("kind: ");
    IF (c.kind < AST.stmtAssign) OR (c.kind > AST.stmtEmpty) THEN
      GenStr("INVALID_STMT");
    ELSE
      CASE c.kind OF
        AST.stmtAssign: GenStr("stmtAssign")
        | AST.stmtProcCall: GenStr("stmtProcCall")
        | AST.stmtIf: GenStr("stmtIf")
        | AST.stmtWhile: GenStr("stmtWhile")
        | AST.stmtRepeat: GenStr("stmtRepeat")
        | AST.stmtFor: GenStr("stmtFor")
        | AST.stmtCase: GenStr("stmtCase")
        | AST.stmtCaseItem: GenStr("stmtCaseItem")
        | AST.stmtElsIf: GenStr("stmtElsIf")
        | AST.stmtElse: GenStr("stmtElse")
        | AST.stmtEmpty: GenStr("stmtEmpty")
      END
    END;
    GenLn;
    IF c.cond # NIL THEN
      GenI; GenStr("cond:"); GenLn; INC(indent);
      GenExpr(l, c.cond);
      DEC(indent);
    END;
    IF c.elsifs # NIL THEN
      GenI; GenStr("elsifs:"); GenLn; INC(indent);
      GenStmt(l, c.elsifs);
      DEC(indent);
    END;
    INC(i);
    c := c.next
  END
END GenStmt;

PROCEDURE Generate*(l :Lex.Lexer; d :AST.Decl);
VAR
  i :INTEGER;
  found :BOOLEAN;
  pv :AST.Decl;
BEGIN
  i := 0; found := FALSE;
  GenI;
  WHILE (i < lastDecl) & ~found DO
    IF decls[i] = d THEN
      found := TRUE;
      GenStr("Already generated decl: "); GenInt(i); GenLn;
    END;
    INC(i)
  END;
  IF ~found THEN
    ASSERT(i < DECL_SIZE);
    decls[i] := d;
    GenStr("Declaration #");
    GenInt(i);
    GenLn;
    GenI;
    INC(lastDecl);
    (* New decl, so diplay it *)
    GenStr("kind: ");
    GenDeclKind(d.kind);
    GenI;
    GenStr("name: ");
    GenQName(l, d.qname);
    GenLn;
    IF d.typ # NIL THEN
      GenI;
      GenStr("type:");
      GenLn;
      INC(indent);
      GenType(l, d.typ);
      DEC(indent)
    END;
    IF d.procVars # NIL THEN
      ASSERT(d.kind = AST.declProcedure);
      GenI; GenStr("procVars:"); GenLn; INC(indent);
      i := 0; pv := d.procVars;
      WHILE (pv # NIL) & (pv.kind # AST.declEND) DO
        GenI; GenStr("procVar #"); GenInt(i); GenLn;
        GenDecl(l, pv);
        pv := pv.next; INC(i)
      END;
      DEC(indent)
    END;
    GenI;
    GenStr("nesting: ");
    GenInt(d.nesting);
    GenLn;
    IF d.body # NIL THEN
      GenI; GenStr("body:"); GenLn; INC(indent);
      GenStmt(l, d.body);
      DEC(indent)
    END;
    IF d.expr # NIL THEN
      GenI; GenStr("expr:"); GenLn; INC(indent);
      GenExpr(l, d.expr);
      DEC(indent)
    END
  END
END Generate;

BEGIN
  indent := 0;
  lastDecl := 0;
  lastType := 0;
  GenDecl := Generate;
END DumpDecl.
