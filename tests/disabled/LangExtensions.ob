(*
 * This file is part of oberon-compiler
 *
 * Copyright (C) 2018-2026  Eric Scharff
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <https://www.gnu.org/licenses/>.
 *
 *)

MODULE LangExtensions;

(* Examples using laguage extensions *)

IMPORT Out;

CONST
  Seven = 7;

TYPE
  BaseMessage = RECORD END;
  SubMessage1 = RECORD (BaseMessage) END;
  SubMessage2 = RECORD (BaseMessage) END;
  SubSubMessage1 = RECORD (SubMessage1) END;

  PBaseMessage = POINTER TO BaseMessage;
  PSub1Message = POINTER TO SubMessage1;
  PSub2Message = POINTER TO SubMessage2;
  PSubSub1Message = POINTER TO SubSubMessage1;

  PtrHolder = RECORD p: PBaseMessage END;

  IntBuf = ARRAY * OF INTEGER;
  RecBuf = RECORD z: INTEGER; b: ARRAY * OF INTEGER END;
  Pair = RECORD a, b: INTEGER END;
  PairBuf = ARRAY * OF Pair;
  OopBuf = ARRAY * OF PtrHolder;
  CallbackFunc = PROCEDURE (i: INTEGER);

PROCEDURE FactIter*(n: INTEGER): INTEGER;
BEGIN
  VAR i := 1;
  VAR r := 1;
  WHILE i <= n DO
    r := r * i;
    INC(i)
  END;
  RETURN r
END FactIter;

PROCEDURE FactRec*(n: INTEGER): INTEGER;
VAR r: INTEGER;
BEGIN
  IF n <= 1 THEN
    r := 1
  ELSE
    r := n * FactRec(n-1)
  END;
  RETURN r
END FactRec;

PROCEDURE FibIter*(n: INTEGER): INTEGER;
BEGIN
  VAR prev := 1;
  VAR fib := 1;
  VAR newPrev := -1;
  WHILE n > 1 DO
    newPrev := fib;
    fib := fib + prev;
    prev := newPrev;
    DEC(n)
  END
  RETURN fib
END FibIter;

PROCEDURE FibRec*(n: INTEGER): INTEGER;
VAR r: INTEGER;
BEGIN
  IF n < 2 THEN
    r := 1
  ELSE
    r := FibRec(n-1) + FibRec(n-2)
  END
  RETURN r
END FibRec;

PROCEDURE TestFib;
VAR
  n: INTEGER;
BEGIN
  n := 7;
  ASSERT(FactRec(n) = FactIter(n));
  ASSERT(FactIter(Seven) = FactIter(n));
  ASSERT(FactIter(7) = 5040);
  Out.Int(FactRec(n)); Out.Ln;
  ASSERT(FibIter(20) = FibRec(20));
  ASSERT(FibRec(20) = 10946);
  Out.Int(FibIter(20)); Out.Ln;
END TestFib;

PROCEDURE DumpBuffer(b: ARRAY OF INTEGER);
VAR
  i: INTEGER;
BEGIN
  i := 0;
  WHILE i < LEN(b) DO
    Out.Int(b[i]); Out.Str(9X);
    INC(i)
  END;
  Out.Ln
END DumpBuffer;

PROCEDURE AddToBuf(VAR b: IntBuf; val: INTEGER);
BEGIN
  PUSH(b, val)
END AddToBuf;

PROCEDURE SumPairs(pairs: ARRAY OF Pair): INTEGER;
BEGIN
  VAR sum := 0;
  VAR l := LEN(pairs) - 1;
  WHILE l >= 0 DO
    sum := sum + pairs[l].a + pairs[l].b;
    DEC(l)
  END
  RETURN sum
END SumPairs;

PROCEDURE TestBuffers;
VAR
  b:  ARRAY * OF INTEGER;
  p:  PairBuf;
  pa: Pair;
  b2: IntBuf;
  i:  INTEGER;
  r:  RecBuf;
BEGIN
  NEW_ARRAY(p, 25);
  pa.a := 11; pa.b := 12;
  PUSH(p, pa);
  pa.a := 13; pa.b := 14;
  PUSH(p, pa);
  pa.a := 15; pa.b := 16;
  PUSH(p, pa);
  Out.Int(11 + 12 + 13 + 14 + 15 + 16); Out.Ln;
  Out.Int(SumPairs(p)); Out.Ln;
  i := SumPairs(p);
  ASSERT(i = 11 + 12 + 13 + 14 + 15 + 16);
  NEW_ARRAY(b, 4);
  FOR i := 1 TO 20 DO
    PUSH(b, i*5)
  END;
  DumpBuffer(b);
  NEW_ARRAY(b2, 4);
  FOR i := 1 TO 20 DO
    AddToBuf(b2, i*5);
  END;
  DumpBuffer(b2);
  NEW_ARRAY(r.b, 400);
  FOR i := 1 TO 20 DO
    PUSH(r.b, i*5)
  END;
  DumpBuffer(r.b);
  FOR i := 0 TO 19 DO
    ASSERT(b[i] = b2[i]);
    ASSERT(b2[i] = r.b[i]);
    ASSERT(r.b[i] = (i+1)*5);
  END;
END TestBuffers;

PROCEDURE TestOopBuffers;
VAR
  b: OopBuf;
  p: PtrHolder;
  pBase: PBaseMessage;
  pSub1: PSub1Message;
  pSub2: PSub2Message;
  pSubSub1: PSubSub1Message;
BEGIN
  NEW_ARRAY(b, 4);
  p.p := NIL;

  NEW(pBase);
  NEW(pSub1);
  NEW(pSub2);
  NEW(pSubSub1);

  p.p := pBase;
  PUSH(b, p);
  p.p := pSub1;
  PUSH(b, p);
  p.p := pSub2;
  PUSH(b, p);
  p.p := pSubSub1;
  PUSH(b, p);

  ASSERT(b[0].p IS PBaseMessage);
  ASSERT(b[1].p IS PBaseMessage);
  ASSERT(b[2].p IS PBaseMessage);
  ASSERT(b[3].p IS PBaseMessage);

  ASSERT(~(b[0].p IS PSub1Message));
  ASSERT(b[1].p IS PSub1Message);
  ASSERT(~(b[2].p IS PSub1Message));
  ASSERT(b[3].p IS PSub1Message);

  ASSERT(~(b[0].p IS PSub2Message));
  ASSERT(~(b[1].p IS PSub2Message));
  ASSERT(b[2].p IS PSub2Message);
  ASSERT(~(b[3].p IS PSub2Message));

  ASSERT(~(b[0].p IS PSubSub1Message));
  ASSERT(~(b[1].p IS PSubSub1Message));
  ASSERT(~(b[2].p IS PSubSub1Message));
  ASSERT(b[3].p IS PSubSub1Message);
END TestOopBuffers;

PROCEDURE TestBitops;
VAR
  i, j: INTEGER;
BEGIN
  i := 2;
  j := 3;
  ASSERT(i BITAND j = 2);
  ASSERT(i BITOR j = 3);
  ASSERT(i BITXOR j = 1);
END TestBitops;

PROCEDURE RunTests*;
BEGIN
  TestBuffers;
  TestOopBuffers;
  TestBitops;
  TestFib
END RunTests;

BEGIN
  RunTests
END LangExtensions.
